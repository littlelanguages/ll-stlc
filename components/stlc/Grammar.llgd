uses "./Scanner.llld";

Program
    : Expression
    ;

Expression
    : Relational {Relational}
    ;

Relational
    : Additive ["==" Additive]
    ;

Additive
    : Multiplicative {AdditiveOps Multiplicative}
    ;

AdditiveOps
    : "+"
    | "-"
    ;

Multiplicative
    : Factor {MultiplicativeOps Factor}
    ;

MultiplicativeOps
    : "*"
    | "/"
    ;

Factor
    : "(" Expression ")"
    | LiteralInt
    | "True"
    | "False"
    | "\" Identifier {Identifier} "->" Expression
    | "let" ["rec"] Declaration {";" Declaration} "in" Expression
    | "if" "(" Expression ")" Expression "else" Expression
    | Identifier
    ;

Declaration
    : Identifier {Identifier} "=" Expression
    ;